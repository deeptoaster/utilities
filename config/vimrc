set autoindent
set clipboard=unnamedplus
set colorcolumn=80
set cursorline
set directory=/tmp
set expandtab
set foldmethod=syntax
set hlsearch
set incsearch
set mouse=a
set number
set sessionoptions-=blank sessionoptions-=buffers sessionoptions-=options
set shell+=\ -O\ globstar
set shiftwidth=2
set smartindent
set tabstop=2
set wildmenu
colorscheme sierra
highlight Normal ctermbg=NONE
let g:airline#extensions#branch#format = 2
let g:airline#extensions#tabline#enabled = 1
let g:airline_powerline_fonts = 1
let g:ale_completion_enabled = 1
let g:ale_fixers = {
  \ 'c': ['clang-format'],
  \ 'cpp': ['clang-format'],
  \ 'cuda': ['clang-format'],
  \ 'python': ['black'],
  \ '*': ['prettier']
\ }
let g:ale_fix_on_save = 1
let g:ale_set_balloons = 1
let g:ale_virtualtext_cursor = 0
let g:ctrlp_custom_ignore =
    \ '\v[\/](\.catkin_tools|\.clangd\/index|\.git|__pycache__|build|devel|dist|logs|node_modules)$'
let g:ctrlp_show_hidden = 1
let g:ctrlp_working_path_mode = 0
let g:DevIconsEnableFoldersOpenClose = 1
let g:NERDTreeGitStatusConcealBrackets = 1
let g:NERDTreeGitStatusUseNerdFonts = 1
let g:NERDTreeShowHidden = 1
packloadall
packadd CtrlP
packadd ale
packadd nerdtree-git-plugin
packadd vim-devicons
packadd nerdtree
packadd vim-surround
function BufClean()
  for l:bufnr in range(1, bufnr('$'))
    if buflisted(l:bufnr) && !bufloaded(l:bufnr)
      execute 'bdelete' l:bufnr
    endif
  endfor
endfunction
function s:FugitiveClose()
  for l:bufnr in range(1, bufnr('$'))
    if getbufvar(l:bufnr, 'fugitive_type') ==# 'index'
      execute 'bdelete' l:bufnr
    endif
  endfor
endfunction
function GoToDefinition()
  try
    execute "normal! \<C-]>"
  catch
    ALEGoToDefinition
  endtry
endfunction
function NERDTreeFugitiveFind()
  let l:bufnr = bufnr()
  if !g:NERDTree.IsOpen()
    call NERDTreeFugitiveToggle()
  endif
  execute l:bufnr . 'bufdo NERDTreeFind'
  if !empty(FugitiveGitDir(l:bufnr))
    wincmd j
    execute 'buffer' l:bufnr
    Gedit :
  endif
endfunction
function NERDTreeFugitiveToggle()
  let l:bufnr = bufnr()
  if g:NERDTree.IsOpen()
    NERDTreeClose
    call s:FugitiveClose()
  else
    NERDTreeFocus
    if !empty(FugitiveGitDir(l:bufnr))
      execute 'belowright sbuffer' l:bufnr
      Gedit :
    endif
  endif
endfunction
function s:PersistWithoutTerminals()
  if !empty(v:this_session) && empty(filter(range(1, bufnr('$')), {
    \ idx, val ->
        \ getbufvar(val, '&modified') &&
        \ getbufvar(val, '&buftype') !=# 'terminal'
  \ }))
    call s:FugitiveClose()
    execute 'mksession!' v:this_session
    sleep 100m
    for l:bufnr in range(1, bufnr('$'))
      if getbufvar(l:bufnr, '&buftype') ==# 'terminal'
        execute 'bdelete!' l:bufnr
      endif
    endfor
  endif
endfunction
augroup squiffles
  autocmd User ALECompletePost ALEHover
  autocmd ExitPre * call s:PersistWithoutTerminals()
augroup end
command BC :call BufClean()
cabbrev bc BC
command GLAG :Git log --all --graph
cabbrev glag GLAG
noremap <C-LeftMouse> <LeftMouse>:call GoToDefinition()<CR>
noremap <Leader>t :call NERDTreeFugitiveToggle()<CR>
noremap <Leader>f :call NERDTreeFugitiveFind()<CR>
noremap <C-]> :call GoToDefinition()<CR>
noremap [g :GitGutterPrevHunk<CR>
noremap ]g :GitGutterNextHunk<CR>
noremap [q :try \| cprev \| catch \| try \| clast \| catch \| ALEPreviousWrap \| endtry \| endtry<CR>
noremap ]q :try \| cnext \| catch \| try \| cfirst \| catch \| ALENextWrap \| endtry \| endtry<CR>
